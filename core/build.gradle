buildscript {
	repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.badlogicgames.gdx:gdx-tools:1.9.4'
    }
}

apply plugin: "java"
apply plugin: 'maven-publish'

def generatedResources = "$projectDir/res"

sourceCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/", "res/" ]
sourceSets.test.java.srcDirs = [ "test/" ]

sourceSets {
  main {
    output.dir(generatedResources, builtBy: 'generateAssets')
  }
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker

task generateAssets() {
	doLast{
		TexturePacker.main("$projectDir/assets", generatedResources, "uiskin")
	}
}

import com.badlogic.gdx.tools.texturepacker.TextureUnpacker

task unpack(){
	doLast{
		TextureUnpacker.main("$projectDir/src/data/uiskin.atlas", "$projectDir/src/data", "$projectDir/assets")
	}
}

eclipse.project {
    name = appName + "-core"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        api(MavenPublication) {
            groupId groupName
            artifactId appName
            
            from components.java
            
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
