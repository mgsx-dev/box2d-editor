apply plugin: "java"

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.test.java.srcDirs = [ "test/", "tutorial/" ]
sourceSets.test.resources.srcDirs = [ "test-resources/", "tutorial-resources/" ]

project.ext.mainClassName = "net.mgsx.box2d.editor.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../demo-core/assets");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir);
 
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

task genTutorials(dependsOn: classes, type: JavaExec, group: "kit") {
    main = "net.mgsx.game.tutorials.TutorialGenerator"
    args = ["-jekyll", "-path", "$projectDir/../../gdx-kit-ghpages/tuto"]
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = false
}


dist.dependsOn classes

eclipse {
    project {
        name = appName + "-demo-desktop"
    }
}
